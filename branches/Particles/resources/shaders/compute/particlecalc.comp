
//#extension GL_ARB_compute_variable_group_size: enable
#define WORK_GROUP_SIZE 128

struct ParticleState
{
	vec4 pos;
	vec4 vel;
	vec4 rot;
	vec4 accLife;
};

layout(std430, binding=4) buffer source 
{
	ParticleState settings[]; // array of structures
} pSettings;

layout(std430, binding=5) buffer StartSettings 
{
	ParticleState settings[]; // array of structures
} startSettings;

uniform sampler2D depthMap;

const float DT = 0.016;

layout( local_size_x = WORK_GROUP_SIZE,  local_size_y = 1, local_size_z = 1)   in;
void main()
{
	uint gid = gl_GlobalInvocationID.x;
	
	if(pSettings.settings[gid].accLife.w <= 0)
	{
		pSettings.settings[gid] = startSettings.settings[gid];
	}
	else
	{
		vec3 p  = pSettings.settings[gid].pos.xyz;
		vec3 v  = pSettings.settings[gid].vel.xyz;
		vec3 pp = p + (v*DT + 0.5*DT*DT*pSettings.settings[gid].accLife.xyz);
		vec3 vp = v + pSettings.settings[gid].accLife.xyz*DT;
		pSettings.settings[gid].pos = vec4(pp,1.0);
		pSettings.settings[gid].vel = vec4(vp,1.0);
		pSettings.settings[gid].accLife.w -= DT;
	}

}

